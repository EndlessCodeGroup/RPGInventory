plugins {
    id 'ru.endlesscode.bukkitgradle' version '0.8.0'
    id 'com.github.johnrengelman.shadow' version '4.0.1'
    id 'jacoco'
}

apply from: 'gradle/nullability.gradle'

group = 'ru.endlesscode'
version = pluginVersion
description = pluginDesc

bukkit {
    version = '1.12.2'

    meta {
        main = "ru.endlesscode.rpginventory.RPGInventoryPlugin"
        authors = ["OsipXD", "EndlessCode Group"]
    }

    run {
        core = "paper"
        eula = true
    }
}

repositories {
    flatDir { dirs 'libs' }

    jcenter()
    dmulloy2()
    md5()
    vault()
    placeholderapi()

    maven {
        name 'keyle-repo'
        url 'http://repo.keyle.de/'
    } 

    maven {
        name 'ec-repo'
        url 'https://gitlab.com/endlesscodegroup/mvn-repo/raw/master/'
    }
}

shadowJar {
    minimize()
    relocate 'com.comphenix.packetwrapper', 'ru.endlesscode.rpginventory.shade.com.comphenix.packetwrapper'
    relocate 'ru.endlesscode.inspector', 'ru.endlesscode.rpginventory.inspector'
}

ext.inspectorVersion = "0.7.1"

dependencies {
    compileOnly(bukkit())
    compileOnly('net.milkbowl.vault:VaultAPI:1.6') {
        exclude group: 'org.bukkit'
    }
    compileOnly('com.comphenix.protocol:ProtocolLib-API:4.4.0')
    compileOnly('org.jetbrains:annotations:15.0')
    compileOnly('me.clip:placeholderapi:2.6.0')
    compileOnly('me.robin:BattleLevelsAPI:6.9.1')
    compileOnly('com.herocraftonline.heroes:Heroes:1.5.5.6')
    compileOnly('de.keyle:mypet:2.3.3')
    compileOnly('org.tobiyas:RacesAndClasses:1.2.0')
    compileOnly('me.baks:RPGPlayerLeveling:3.7.2')
    compileOnly('com.sucy:SkillAPI:3.102')
    compileOnly('me.leothepro555:Skills')
    compileOnly("ru.endlesscode.inspector:inspector-bukkit:$inspectorVersion")
    runtime("ru.endlesscode.inspector:inspector-bukkit-runtime:$inspectorVersion")
    implementation('com.comphenix.packetwrapper:PacketWrapper:1.12.1-R0.1-SNAPSHOT')
    testImplementation('junit:junit:4.12')
    testImplementation('org.mockito:mockito-core:2.8.+')
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

// Add custom tasks
jar.dependsOn shadowJar
check.dependsOn jacocoTestReport
