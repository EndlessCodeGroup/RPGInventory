plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'ru.endlesscode.bukkitgradle' version '0.9.2'
    id 'com.github.ben-manes.versions' version "0.36.0"
    id "com.jfrog.bintray" version "1.8.5" apply false
    id 'jacoco'
}

group = 'ru.endlesscode.rpginventory'
version = pluginVersion
description = pluginDesc

bukkit {
    apiVersion = '1.16.4'

    meta {
        name.set("RPGInventory")
        main.set("${group}.RPGInventoryPlugin")
        apiVersion.set("1.14")
        authors.set(["osipxd", "EndlessCode Group"])
    }

    server {
        core = "paper"
        eula = true
    }
}

// Replace sentry credentials in code
def processSources = tasks.register("processSource", Sync) {
    // The key should be declared in ~/.gradle/gradle.properties
    def dsn = findProperty("rpginvSentryDsn") ?: ""

    from(sourceSets.main.java)
    inputs.property("dsn", dsn)
    filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [sentry_dsn: dsn])
    into("$buildDir/src")
}

compileJava {
    source = processSources.get().outputs
}

repositories {
    flatDir { dirs 'libs' }

    jcenter()
    spigot()
    dmulloy2()
    md5()
    placeholderapi()
    jitpack()
    codemc()

    maven {
        name 'keyle-repo'
        url 'http://repo.keyle.de/'
    }

    maven {
        name 'ec-repo'
        url 'https://gitlab.com/endlesscodegroup/mvn-repo/raw/master/'
    }

    maven { setUrl('https://dl.bintray.com/endlesscode/repo') }
}

shadowJar {
    minimize()

    def shadowPackage = "shade.ru.endlesscode.rpginventory"
    relocate "com.comphenix.packetwrapper", "${shadowPackage}.packetwrapper"

    // inspector
    relocate "ru.endlesscode.inspector", "${shadowPackage}.inspector"
    relocate "org.jetbrains", "${shadowPackage}.jetbrains"
    relocate "kotlinx", "${shadowPackage}.kotlinx"
    relocate "kotlin", "${shadowPackage}.kotlin"

    // inspector-sentry-reporter
    relocate "io.sentry", "${shadowPackage}.sentry"
    relocate "org.slf4j", "${shadowPackage}.slf4j"
    relocate "com.fasterxml.jackson.core", "${shadowPackage}.jackson"

    // bstats
    relocate "org.bstats.bukkit", "${shadowPackage}.bstats"
}

def inspectorVersion = "0.10.1"

dependencies {
    compileOnly(spigotApi())
    compileOnly('com.github.MilkBowl:VaultAPI:1.7') {
        exclude group: 'org.bukkit'
    }
    compileOnly('com.comphenix.protocol:ProtocolLib:4.5.0')
    compileOnly('org.jetbrains:annotations:18.0.0')
    compileOnly('me.clip:placeholderapi:2.10.4')
    compileOnly('me.robin:battlelevels-api:6.9.1')
    compileOnly('com.herocraftonline.heroes:Heroes:1.5.5.7')
    compileOnly('de.keyle:mypet:3.8')
    compileOnly('org.tobiyas:racesandclasses:1.2.0')
    compileOnly('me.baks:rpgplayerleveling:3.7.2')
    compileOnly('com.sucy:SkillAPI:3.102')
    compileOnly('me.leothepro555:skills:12.1.7')
    compileOnly('ru.endlesscode.mimic:mimic-bukkit-api:0.5')
    implementation("ru.endlesscode.inspector:inspector-bukkit:$inspectorVersion")
    implementation("ru.endlesscode.inspector:inspector-sentry-reporter:$inspectorVersion")
    implementation("ru.endlesscode.inspector:sentry-bukkit:$inspectorVersion")
    implementation('com.comphenix.packetwrapper:PacketWrapper:1.13-R0.1-SNAPSHOT')
    implementation('org.bstats:bstats-bukkit-lite:1.5')
    testImplementation('junit:junit:4.12')
    testImplementation('org.mockito:mockito-core:3.2.0')
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

// Add custom tasks
tasks.assemble.dependsOn tasks.shadowJar
tasks.check.dependsOn tasks.jacocoTestReport

// Add ProGuard minimization task
// TODO: minimized Jar not works, fix it
//apply(from: file("proguard.gradle"))

apply(from: file("gradle/publish.gradle"))
