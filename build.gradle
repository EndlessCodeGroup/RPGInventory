plugins {
    id 'ru.endlesscode.bukkitgradle' version '0.8.2'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'com.github.ben-manes.versions' version "0.27.0"
    id 'jacoco'
}

group = 'ru.endlesscode.rpginventory'
version = pluginVersion
description = pluginDesc

bukkit {
    version = '1.15.2'

    meta {
        name = "RPGInventory"
        main = "${group}.RPGInventoryPlugin"
        authors = ["osipxd", "EndlessCode Group"]
    }

    run {
        core = "spigot"
        eula = true
    }
}

// Replace sentry credentials in code
def processSources = tasks.register("processSource", Sync) {
    def projectId = "1331962"
    // The key should be declared in ~/.gradle/gradle.properties
    def key = findProperty("rpginvSentryKey") ?: ""

    from(sourceSets.main.java)
    inputs.property("projectId", projectId)
    inputs.property("key", key)
    filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [sentry_key: key, sentry_project_id: projectId])
    into("$buildDir/src")
}

compileJava {
    source = processSources.get().outputs
}

repositories {
    flatDir { dirs 'libs' }

    jcenter()
    spigot()
    dmulloy2()
    md5()
    vault()
    placeholderapi()

    maven {
        name 'keyle-repo'
        url 'http://repo.keyle.de/'
    }

    maven {
        name 'ec-repo'
        url 'https://gitlab.com/endlesscodegroup/mvn-repo/raw/master/'
    }

    maven {
        name 'codemc-repo'
        url 'https://repo.codemc.org/repository/maven-public/'
    }
}

shadowJar {
    minimize()

    def shadowPackage = "shade.ru.endlesscode.rpginventory"
    relocate "com.comphenix.packetwrapper", "${shadowPackage}.packetwrapper"

    // inspector
    relocate "ru.endlesscode.inspector", "${shadowPackage}.inspector"
    relocate "org.jetbrains", "${shadowPackage}.jetbrains"
    relocate "kotlinx", "${shadowPackage}.kotlinx"
    relocate "kotlin", "${shadowPackage}.kotlin"

    // inspector-sentry-reporter
    relocate "io.sentry", "${shadowPackage}.sentry"
    relocate "org.slf4j", "${shadowPackage}.slf4j"
    relocate "com.fasterxml.jackson.core", "${shadowPackage}.jackson"

    // bstats
    relocate "org.bstats.bukkit", "${shadowPackage}.bstats"
}

ext.inspectorVersion = "0.9"

dependencies {
    compileOnly(bukkit()) {
        exclude group: 'junit'
        exclude module: 'json'
    }
    compileOnly('net.milkbowl.vault:VaultAPI:1.7') {
        exclude group: 'org.bukkit'
    }
    compileOnly('com.comphenix.protocol:ProtocolLib:4.5.0')
    compileOnly('org.jetbrains:annotations:18.0.0')
    compileOnly('me.clip:placeholderapi:2.10.4')
    compileOnly('me.robin:battlelevels-api:6.9.1')
    compileOnly('com.herocraftonline.heroes:Heroes:1.5.5.7')
    compileOnly('de.keyle:mypet:3.8')
    compileOnly('org.tobiyas:racesandclasses:1.2.0')
    compileOnly('me.baks:rpgplayerleveling:3.7.2')
    compileOnly('com.sucy:SkillAPI:3.102')
    compileOnly('me.leothepro555:skills:12.1.7')
    implementation("ru.endlesscode.inspector:inspector-bukkit:$inspectorVersion")
    implementation("ru.endlesscode.inspector:inspector-sentry-reporter:$inspectorVersion")
    implementation("ru.endlesscode.inspector:sentry-bukkit:$inspectorVersion")
    implementation('com.comphenix.packetwrapper:PacketWrapper:1.13-R0.1-SNAPSHOT')
    implementation('org.bstats:bstats-bukkit-lite:1.5')
    testImplementation('junit:junit:4.12')
    testImplementation('org.mockito:mockito-core:3.2.0')
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

// Add custom tasks
tasks.assemble.dependsOn tasks.shadowJar
tasks.check.dependsOn tasks.jacocoTestReport

// Add ProGuard minimization task
// TODO: minimized Jar not works, fix it
//apply(from: file("proguard.gradle"))

apply(from: file("gradle/publish.gradle"))
