plugins {
    id 'ru.endlesscode.bukkitgradle' version '0.7.0'
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id 'jacoco'
}

group = 'ru.endlesscode'
version = pluginVersion
description = pluginDesc

bukkit {
    version = '1.12.2'
    buildtools = 'build/buildtools/BuildTools.jar'
    meta.authors = ["OsipXD", "EndlessCode Group"]

    run {
        eula = true
        dir = '../DevServer/'
    }
}

repositories {
    flatDir { dirs 'libs' }

    maven {
        name 'dmulloy2-repo'
        url 'http://repo.dmulloy2.net/nexus/repository/public/'
    }

    maven {
        name 'md5-public'
        url 'http://repo.md-5.net/content/groups/public/'
    }

    maven {
        name 'vault-repo'
        url 'http://nexus.hc.to/content/repositories/pub_releases/'
    }

    // TODO: Check that it works
    //maven {
    //    name 'keyle-repo'
    //    url 'https://nexus.keyle.de/content/groups/public/'
    //}

    maven {
        name 'placeholderapi'
        url 'http://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

shadowJar {
    relocate 'com.comphenix.packetwrapper', 'ru.endlesscode.rpginventory.shade.com.comphenix.packetwrapper'
}

dependencies {
    compileOnly bukkit()
    compileOnly (group: 'net.milkbowl.vault', name: 'VaultAPI', version: '1.6') {
        exclude group: 'org.bukkit'
    }
    compileOnly group: 'com.comphenix.protocol', name: 'ProtocolLib-API', version: '4.4.0-SNAPSHOT'
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '15.0'
    compileOnly group: 'me.clip', name: 'placeholderapi', version: '2.6.0'
    compileOnly group: 'me.robin', name: 'BattleLevelsAPI', version: '6.9.1'
    compileOnly group: 'com.herocraftonline', name: 'Heroes', version: '1.5.5.7'
    compileOnly group: 'de.keyle', name: 'MyPet', version: '2.3.2-SNAPSHOT'
    compileOnly group: 'org.tobiyas', name: 'RacesAndClasses', version: '1.2.0'
    compileOnly group: 'me.baks', name: 'RPGPlayerLeveling', version: '3.3.1'
    compileOnly group: 'com.sucy', name: 'SkillAPI', version: '3.90'
    compileOnly group: 'me.leothepro555', name: 'Skills'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

// Add custom tasks
jar.dependsOn shadowJar
check.dependsOn jacocoTestReport
