plugins {
    id 'ru.endlesscode.bukkitgradle' version '0.8.1'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'com.github.ben-manes.versions' version "0.21.0"
    id 'jacoco'
}

group = 'ru.endlesscode'
version = pluginVersion
description = pluginDesc

bukkit {
    version = '1.12.2'

    meta {
        main = "ru.endlesscode.rpginventory.RPGInventoryPlugin"
        authors = ["OsipXD", "EndlessCode Group"]
    }

    run {
        core = "paper"
        eula = true
    }
}

repositories {
    flatDir { dirs 'libs' }

    jcenter()
    dmulloy2()
    md5()
    vault()
    placeholderapi()

    maven {
        name 'keyle-repo'
        url 'http://repo.keyle.de/'
    } 

    maven {
        name 'ec-repo'
        url 'https://gitlab.com/endlesscodegroup/mvn-repo/raw/master/'
    }
}

shadowJar {
    minimize()
    
    def shadowPackage = "shade.ru.endlesscode.rpginventory"
    relocate "com.comphenix.packetwrapper", "${shadowPackage}.packetwrapper"
    
    // inspector
    relocate "ru.endlesscode.inspector", "${shadowPackage}.inspector"
    relocate "org.jetbrains.annotations", "${shadowPackage}.annotations"
    relocate "kotlinx", "${shadowPackage}.kotlinx"
    relocate "kotlin", "${shadowPackage}.kotlin"

    // inspector-sentry-reporter
    relocate "io.sentry", "${shadowPackage}.sentry"
    relocate "org.slf4j", "${shadowPackage}.slf4j"
    relocate "com.fasterxml.jackson.core", "${shadowPackage}.jackson"
}

ext.inspectorVersion = "0.8.1"

dependencies {
    compileOnly(bukkit()) {
        exclude group: 'junit'
        exclude module: 'gson'
        exclude module: 'snakeyaml'
    }
    compileOnly('net.milkbowl.vault:VaultAPI:1.6') {
        exclude group: 'org.bukkit'
    }
    compileOnly('com.comphenix.protocol:ProtocolLib-API:4.4.0')
    compileOnly('org.jetbrains:annotations:15.0')
    compileOnly('me.clip:placeholderapi:2.9.2')
    compileOnly('me.robin:BattleLevelsAPI:6.9.1')
    compileOnly('com.herocraftonline.heroes:Heroes:1.5.5.7')
    compileOnly('de.keyle:mypet:2.3.3')
    compileOnly('org.tobiyas:RacesAndClasses:1.2.0')
    compileOnly('me.baks:RPGPlayerLeveling:3.7.2')
    compileOnly('com.sucy:SkillAPI:3.102')
    compileOnly('me.leothepro555:Skills')
    implementation("ru.endlesscode.inspector:inspector-bukkit:$inspectorVersion")
    implementation("ru.endlesscode.inspector:inspector-sentry-reporter:$inspectorVersion")
    implementation('com.comphenix.packetwrapper:PacketWrapper:1.12.1-R0.1-SNAPSHOT')
    testImplementation('junit:junit:4.12')
    testImplementation('org.mockito:mockito-core:2.25.0')
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

// Add custom tasks
tasks.assemble.dependsOn tasks.shadowJar
tasks.check.dependsOn tasks.jacocoTestReport

// Add ProGuard minimization task
// TODO: minimized Jar not works, fix it
apply(from: file("proguard.gradle"))
